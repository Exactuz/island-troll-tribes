package BossAbilities

// Standard lib Imports:
import AbilityObjEditing
import BuffObjEditing
import ObjectIdGenerator
import ObjectIds
import Assets

// Local Imports:
import LocalObjectIDs
import LocalAssets
import ColorUtils
import ToolTipsUtils
import DamageEvent
import DamageListeners
import LinkedList
import ClosureTimers
import Orders
import ClosureEvents
import OnUnitEnterLeave
import MakruraCuirass
import HashList


let ACID_BREATH_DMG_SEC = 20.
let ACID_BREATH_DURATION = 10.

@compiletime function createBreathOfAcid() returns AbilityDefinitionBreathofFrostCreep
    let buffId = BUFF_ID_GEN.next()
    new BuffDefinition(buffId, BuffIds.breathofFrost)
        ..setIconNormal(1, Icons.bTNCorrosiveBreath)
        ..setTooltipNormal("Poisonous Breath")
        ..setTooltipNormalExtended(
            "This unit was hit by the Breath of a Hydra and will take "+
            "{0} damage over {1} seconds.".format(
                ACID_BREATH_DMG_SEC.toToolTipRed(),
                ACID_BREATH_DURATION.toToolTipLightBlue()
                )
            )
        ..setName("Poisonous Breath")
        ..setArtTarget(Abilities.chimaeraAcidTargetArt)

    return new AbilityDefinitionBreathofFrostCreep(ABILITY_HYDRA_BREATH)
        ..setIconNormal(Icons.bTNCorrosiveBreath)
        ..setBuffs(1, buffId.toRawCode())
        ..setDamagePerSecond(1, ACID_BREATH_DMG_SEC)
        ..setDurationNormal(1, ACID_BREATH_DURATION)
        ..setDurationHero(1, ACID_BREATH_DURATION)
        ..setMissileArt(LocalAbilities.breatOfAcidMissile)
        ..setName("Breath of Hydra")
        ..setTooltipNormal(1, "Breath of Hydra")
        ..setCastRange(1, 800.0)
        ..setTargetsAllowed(1, "terrain")

// the one crit
@compiletime function createCriticalStrike() returns AbilityDefinitionCriticalStrikecreep
    return new AbilityDefinitionCriticalStrikecreep(ABILITY_CRIT)
        ..setDamageMultiplier(1, 1.5)
        ..setItemAbility(true)
        ..setName("Item Crit")

let DMG_REDUCED = 17.
let CHANCE_TO_REDUCE = 0.5

public let ABILITY_HARDENED_SKIN_DUMMY_BUFF = compiletime(
    createDummyBuffObject(
        "Damage Reduction".color(COLOR_GREEN),
        "This unit has hardened skin: granting it a {0} chance to incoming reduce damage by {1}."
        .format(CHANCE_TO_REDUCE.toToolTipGreen(), DMG_REDUCED.toToolTipRed()),
        Icons.bTNHardenedSkin
    ).abilId
)

// Used by bosses
@compiletime function createBossHardenedSkin() returns AbilityDefinitionHardenedSkin
    return new AbilityDefinitionHardenedSkin(ABILITY_HARDENED_SKIN)
        ..setCheckDependencies(false)
        ..setChancetoReduceDamage(1, CHANCE_TO_REDUCE * 100)
        ..setMinimumDamage(1, 1.0)
        ..setIgnoredDamage(1, DMG_REDUCED)
        ..setEditorSuffix("(modded)")

// Used by Disco Duck & The One
@compiletime function createBossEnsnare() returns AbilityDefinitionWeb
    return new AbilityDefinitionWeb(ABILITY_BOSS_ENSNARE)
        ..setIconNormal(Icons.bTNEnsnare)
        ..setBuffs(1, "Bena,Beng")
        ..setCheckDependencies(false)
        ..setCooldown(1, 10.0)
        ..setDurationNormal(1, 8.0)
        ..setEditorSuffix("(Boss Ensnare)")
        ..setDurationHero(1, 2.5)
        ..setMissileArt(Abilities.ensnaremissile)
        ..setCastRange(1, 700.0)
        ..presetTargetsAllowed(_ -> commaList(
            TargetsAllowed.air,
            TargetsAllowed.enemies,
            TargetsAllowed.ground,
            TargetsAllowed.neutral,
            TargetsAllowed.nonancient,
            TargetsAllowed.organic
            )
        )
        ..setTooltipNormal(1, "[|cffffd700R|r] Ensnare")
        ..setTooltipNormalExtended(1, "Binds an enemy target for 2.50/8 seconds on heroes/normal units.")
        ..setIconTurnOff(Icons.bTNEnsnare)

let SPELL_RESISTANCE = 0.3

public let ABILITY_SPELL_DMG_REDUCTION_DUMMY_BUFF = compiletime(
    createDummyBuffObject(
        "Spell Damage Reduction".color(COLOR_GREEN),
        "This unit has thick skin: reducing all spell damage dealt to it by {0}."
        .format(SPELL_RESISTANCE.toToolTipGreen()),
        Icons.bTNThickFur
    ).abilId
)

let MAKRURA_CARAPACE_BOSS_CD = 7.
public let ABILITY_MAKRURA_CARAPACE_BOSS_BUFF = compiletime(
    createDummyBuffObject(
        "Makrura Carapace".color(COLOR_GREEN),
        "Makrura's impenetrable carapace allows it to block an attack and reflect premitigated damage back to the attacker. "+
        "Has a separate {0} seconds cooldown.".format(MAKRURA_CARAPACE_BOSS_CD.toToolTipBlue()),
        LocalIcons.bTNMakruraCarapace
    ).abilId
)

//personal cd per unit
HashList<unit>makruraCarapaceCooldown = new HashList<unit>()

// Used by Makrura
function onMakruraAttacked(unit source, unit target, real originalAmount)
    //cast Pin
    if makruraCarapaceCooldown.size()>1 // use cuirass ability if attacked by 2 separate units
    and target.getAbilityCooldownRemaining(ABILITY_MAKRURA_CUIRASS_ACTIVE) ==0.
        IssueImmediateOrderById(target, OrderIds.berserk)
        let efx = addEffect("Models\\Abilities\\Bubbles.mdx", target, "origin")
                    ..setScale(2.0)
        doAfter(MAKRURA_CUIRASS_DURATION) -> 
            efx.destr()
    if target.getAbilityCooldownRemaining(ABILITY_MAKRURA_BOSS_PIN)==0.
        target.issueTargetOrderById(OrderIds.entanglingroots, source)
    if makruraCarapaceCooldown.has(source)
        return
    makruraCarapaceCooldown.add(source)
    let currentHp = target.getHP()
    target.setHP(currentHp+originalAmount)
    if not source.isInvulnerable()
        target.damageTarget(source, originalAmount, true, false, ATTACK_TYPE_HERO, DAMAGE_TYPE_NORMAL, null)
        flashEffect("Objects\\Spawnmodels\\Naga\\NagaDeath\\NagaDeath.mdl", source.getPos())
    doAfter(MAKRURA_CARAPACE_BOSS_CD)->
        makruraCarapaceCooldown.remove(source) 

function onMakruraEnter(unit u)
    if u.getTypeId() == UNIT_MAKRURA
        u.removeAbility(ABILITY_HARDENED_SKIN)
        u.removeAbility(ABILITY_HARDENED_SKIN_DUMMY_BUFF)
        new MakruraCuirass(u)      

let MAKRURA_BOSS_PIN_DURATION = 4.
let MAKRURA_BOSS_PIN_DPS      =15.
@compiletime function createMakruraBossPinAbility()
    new AbilityDefinitionEntanglingRootscreep(ABILITY_MAKRURA_BOSS_PIN)
        ..setDummyAbility()
        ..setBuffs(1, BUFF_MAKRURA_PIN.toRawCode())
        ..setMissileArt("")
        ..setArtTarget("")
        ..setArtEffect("")
        ..setAnimationNames("Attack")
        ..setDurationHero(1, MAKRURA_BOSS_PIN_DURATION)
        ..setDurationNormal(1, 8.)
        ..setTargetsAllowed(1, commaList(
            TargetsAllowed.enemies,
            TargetsAllowed.ground,
            TargetsAllowed.neutral,
            TargetsAllowed.organic,
            TargetsAllowed.hero,
            TargetsAllowed.nonhero
            ))
        ..setCastRange(1, 300.)
        ..setCooldown(1, 20.)
        ..setDamageperSecond(1, MAKRURA_BOSS_PIN_DPS)
        ..setName("Pin")
        ..setEditorSuffix("(Wurst)")    

// Used by bosses
@compiletime function createBossSpellResistance() returns AbilityDefinitionRunedBracers
    return new AbilityDefinitionRunedBracers(ABILITY_SPELL_DMG_REDUCTION)
        ..setDamageReduction(1, SPELL_RESISTANCE)
        ..setName("Boss Spell Resistance")
        ..setEditorSuffix("(modded)")


init
    DamageEvent.addListener(DamageListenerPriorities.PANTHER_INSTINCT castTo int) ->     
        if DamageEvent.getTarget().getTypeId() == UNIT_MAKRURA and DamageEvent.getType() == DamageType.ATTACK
            onMakruraAttacked(DamageEvent.getSource(), DamageEvent.getTarget(), DamageEvent.getUnreducedOriginalAmount())
            
    //flasheffect for Pin
    EventListener.onTargetCast(ABILITY_MAKRURA_BOSS_PIN) (unit caster, unit target) ->
        flashEffect(Abilities.stampedeMissileDeath, target.getPos())        
        doPeriodicallyCounted(1., 4) (CallbackCounted cb) ->
            flashEffect(Abilities.stampedeMissileDeath, target.getPos())

    onEnter()->
        onMakruraEnter(getEnterLeaveUnit())        


