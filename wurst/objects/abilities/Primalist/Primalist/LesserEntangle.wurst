package LesserEntangle

// Standard library imports:
import ClosureEvents
import ClosureTimers

// Local imports:
import LocalObjectIDs
import InstantDummyCaster
import Orders
import BuffIds
import ChannelAbilityPreset
import LocalObjectIDs2
import UnitExtensions
import ToolTipsUtils

public constant ROOT_DURATION_HERO = 1.75
public constant ROOT_DURATION_NORMAL = 8.
constant ROOT_COOLDOWN = 20.
public constant ROOT_CAST_RANGE = 450.
constant ROOT_DAMAGE_PERIOD = 1.
constant ROOT_TOTAL_DAMAGE = 20.
constant ROOT_TOTAL_DAMAGE_LVL_4 = 40.
constant ROOT_DAMAGE_PER_SECOND = ROOT_TOTAL_DAMAGE/ROOT_DURATION_NORMAL
constant ROOT_MANACOST = 20
constant PRIMALIST_ROOTS_TT_NORMAL = "Lesser Entangling Roots"
constant PRIMALIST_ROOTS_TT_EXT =  "Causes roots to burst from the ground, immobilizing the target for {0}({1}) seconds.".format(
                                    ROOT_DURATION_NORMAL.toToolTipLightBlue(), ROOT_DURATION_HERO.toToolTipLightBlue())+
                                    "\nInflicts {0} damage to animals over duration.".format(ROOT_TOTAL_DAMAGE.toToolTipRed())+
                                    "\nHas {0} seconds cooldown.".format(ROOT_COOLDOWN.toToolTipLightBlue())+
                                    "\nLevel 4: ".color(COLOR_GOLD_STR)+"damage to animals increased to {0}".format(ROOT_TOTAL_DAMAGE_LVL_4.toToolTipRed())

@compiletime function createEntaglingRootAbility()
    new AbilityDefinitionEnsnareCreep(ABILITY_PRIMALIST_ROOT_TROLL)
        ..setDummyAbility()
        ..setBuffs(1, commaList(BuffIds.entanglingRoots, BuffIds.entanglingRoots))
        ..setMissileArt("")
        ..setDurationHero(1, ROOT_DURATION_HERO)
        ..setDurationNormal(1, ROOT_DURATION_NORMAL)
        ..setTargetsAllowed(1, commaList(
            TargetsAllowed.enemies,
            TargetsAllowed.ground,
            TargetsAllowed.neutral,
            TargetsAllowed.organic
            ))
        ..setName("Primalist Entangling Roots Troll")
        ..setEditorSuffix("(Wurst)")

    new ChannelAbilityPreset(ABILITY_PRIMALIST_ROOT_CAST, 1, true)
        ..setCooldown(1, ROOT_COOLDOWN)
        ..setCastRange(1, ROOT_CAST_RANGE)
        ..setCooldown(1, ROOT_COOLDOWN)
        ..presetIcon(Icons.bTNEntanglingRoots)
        ..presetTargetTypes(Targettype.UNIT)
        ..presetManaCost(lvl->ROOT_MANACOST)
        ..presetBaseOrderID(lvl->"entangle")
        ..setButtonPositionNormalX(2)
        ..setButtonPositionNormalY(1)
        ..setHotkeyNormal("R")
        ..setTooltipNormal(1, PRIMALIST_ROOTS_TT_NORMAL)
        ..setTooltipNormalExtended(1, PRIMALIST_ROOTS_TT_EXT)
        ..setTargetsAllowed(1, commaList(
            TargetsAllowed.ground,
            TargetsAllowed.enemies,
            TargetsAllowed.organic
        ))
        ..setName("Primalist Entangling Roots cast")
    
function onRoot(unit caster, unit target)
    if target.getTypeId() == UNIT_HAWK
        nullTimer()->
            caster.endAbilityCooldown(ABILITY_PRIMALIST_ROOT_CAST)
        return
    InstantDummyCaster.castTarget(caster.getOwner(), ABILITY_PRIMALIST_ROOT_TROLL, 1, Orders.ensnare, target)
    if target.isTroll()
        return
    real totalDamage = 0.
    if target.getLevel() >= 4 or caster.getTypeId() == UNIT_TREANT_SENTRY or caster.getTypeId() == UNIT_TREANT_PROTECTOR
        totalDamage = ROOT_TOTAL_DAMAGE_LVL_4
    else
        totalDamage = ROOT_TOTAL_DAMAGE
    let dmgPerTick = totalDamage/ROOT_DURATION_NORMAL*ROOT_DAMAGE_PERIOD
    doPeriodicallyTimed(ROOT_DAMAGE_PERIOD, ROOT_DURATION_NORMAL) (CallbackCounted cb) ->
        caster.damageTarget(target, dmgPerTick, false, false, ATTACK_TYPE_NORMAL, DAMAGE_TYPE_MAGIC, null)


init
    EventListener.onTargetCast(ABILITY_PRIMALIST_ROOT_CAST, (unit caster, unit target) -> onRoot(caster, target))