package DireWolfDevour

// Standard lib Imports:
import ChannelAbilityPreset
import ClosureEvents
import HashMap
import Assets
import OrderIds
import ErrorHandling
import SoundUtils

// Local Imports:
import UnitEntity
import Hostile
import SimError
import HealingSystem
import PlayerExtensions
import LocalObjectIDs
import ColorUtils
import ToolTipsUtils
import MoveCorpses
import LocalAssets

let DEVOUR_SOUND = new SoundDefinition(Sounds.devour, false, true)

let COOLDOWN = 60.
    
let TT_NORM = "Devour"
let TT_EXT = "" +
    "Completely devour an elk, restoring 3 raw meat." +
    "\nDire wolf swallows up and digests every part of the animal, therefore, no hides " +
    ", bones nor corpses are dropped upon the animal death."+
    "\nHas {0}".format(COOLDOWN.toString().color(COLOR_LIGHT_BLUE))

@compiletime function createDevourSpell() returns ChannelAbilityPreset
    return new ChannelAbilityPreset(ABILITY_DIRE_WOLF_DEVOUR, 1, true)
        ..presetTargetTypes(Targettype.UNIT)
        ..setName(TT_NORM)
        ..setHotkeyNormal("R")
        ..setTooltipNormal(1, TT_NORM)
        ..setTooltipNormalExtended(1, TT_EXT)
        ..presetCooldown(lvl -> COOLDOWN)
        ..presetManaCost(lvl -> 0)
        ..presetCastRange(lvl -> 128)
        ..presetButtonPosNormal(0, 3)
        ..setFollowThroughTime(1, 0)
        ..setIconNormal(LocalIcons.btnDireWolfDevour)
        ..setTargetsAllowed(1, commaList(
            TargetsAllowed.enemies,
            TargetsAllowed.nonhero,
            TargetsAllowed.organic,
            TargetsAllowed.nonancient
        ))
        ..setAnimationNames("stand")
        ..setBaseOrderID(1, "creepdevour")


function onDevourCast(unit caster, unit target)
    if not target.getTypeId() == UNIT_ELK
        simError(caster.getOwner(), "You can only devour elks.")
        caster.issueImmediateOrder("stop")
        return
    // Play a specific animation which... look like rendo's devouring something?
    // Forced to pause unit because channel ability always play an animation
    caster.pause()
    caster.setAnimation(2)
    caster.unpause()

    let entity = UnitEntity.findForUnit(target)
    if entity instanceof Hostile
        let hostile = entity castTo Hostile
        // Disable item & corpses drop
        hostile.setDropOndeath(false)
        // Dealing 120 pure damage to kill the target
        UnitDamageTarget(caster, target, 120., false, false, ATTACK_TYPE_CHAOS, DAMAGE_TYPE_UNIVERSAL, null)
        onDireWolfDevour(caster)

    
    flashEffect(Abilities.vampiricAuraTarget, caster, "origin")
    flashEffect(Objects.humanSmallDeathExplode, target.getPos())


init
    EventListener.onTargetCast(ABILITY_DIRE_WOLF_DEVOUR, (unit caster, unit target) -> onDevourCast(caster, target))
