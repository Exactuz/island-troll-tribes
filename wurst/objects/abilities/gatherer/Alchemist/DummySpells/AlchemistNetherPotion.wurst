package AlchemistNetherPotion

// Standard library imports:
import ClosureEvents
import ChannelAbilityPreset
import Orders
import LinkedList

// Local imports:
import LocalObjectIDs
import ToolTipsUtils
import UnitExtensions
import Lodash
import NetherPotion
import InstantDummyCaster
import SimError
import PhilosophersStone

//constant NAME =""
//constant COOLDOWN = 0
//constant ICON =""
constant POTION_COST = 30
constant errorMsg = "Philosopher's stone doesnt have enough energy"
constant TT = NAME
constant TT_COST = "\nCosts "+POTION_COST.toString().color(COLOR_GOLD_STR) +" energy."
constant TT_EXTENDED =TT_NETHER_POTION+TT_COST
constant POS = new Pair(1, 1)

LinkedList<string>hotKeys = asList("Q","W","E","R","A","S","D","F","Z","X","C")
let hotkeyIndex=POS.b*4+POS.a
constant HOTKEY = hotKeys.get(hotkeyIndex)
constant TARGETS_ALLOWED = commaList(TargetsAllowed.vulnerable,
                                     TargetsAllowed.organic)

@compiletime function createDummyPotionAbility()
    new ChannelAbilityPreset(ABILITY_ALCHEMIST_NETHER_POTION, 1, true)
        ..presetCastingTime(lvl-> 0)
        ..presetManaCost(lvl -> 0)
        ..presetCooldown(lvl ->0)
        ..setHeroAbility(true)
        ..setHotkeyNormal(HOTKEY)
        ..setName(NAME)
        ..presetTooltipNormal(lvl -> makeToolTipNorm(HOTKEY, TT))
        ..presetTooltipNormalExtended(lvl -> TT_EXTENDED)
        ..presetTargetsAllowed(lvl->TARGETS_ALLOWED)            
        ..setIconNormal(ICON)
        ..setIconResearch(ICON)
        ..setIconTurnOff(ICON)
        ..setButtonPositionNormalX(POS.a)
        ..setButtonPositionNormalY(POS.b)
        ..setFollowThroughTime(1, 0)
        ..setEditorSuffix("(Wurst)")
        ..presetCastRange(lvl->CAST_RANGE)
        ..presetTargetTypes(Targettype.UNIT)
        //..setBaseOrderID(1, "acidbomb")


function onCast()
    let target = GetSpellTargetUnit()
    let caster = GetSpellAbilityUnit()
    let instance = instances.get(caster)
    if instance == null
        return
    if not caster.hasItemById(ITEM_PHILOSOPHERS_STONE)
        return
    if instance.spendCharges(POTION_COST)
        InstantDummyCaster.castTarget(caster.getOwner(), ABILITY_NETHER_POTION_BANISH, 1, Orders.banish, target)
    else
        caster.issueImmediateOrder("stop")
        simError(caster.getOwner(), errorMsg)

    


init 
    registerSpellEffectEvent(ABILITY_ALCHEMIST_NETHER_POTION, function onCast)
