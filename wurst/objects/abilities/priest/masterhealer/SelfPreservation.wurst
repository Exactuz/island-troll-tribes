package SelfPreservation

import GameConfig

import ClosureEvents
import ChannelAbilityPreset
import Assets
import LocalObjectIDs
import Lodash
import ToolTipsUtils
import LocalAssets
import ColorUtils
import HealingSystem

let COOLDOWN = 60.
let MANACOST = 0
let BASE_STATS_RAISE_AMOUNT = 10.
let STATS_RAISE_INT_MULTIPLIER = 1.

let TOOLTIP_NORM = "Self Preservation"
let TOOLTIP_EXTENDED = "Raises all stats (Health Point, Energy, Heat) of your troll by {0}+{1}% of your {2}. Has {3} seconds cooldown."
                       .format(BASE_STATS_RAISE_AMOUNT.toToolTipGreen(),(STATS_RAISE_INT_MULTIPLIER*100).toToolTipGreen(), 
                       "Intelligence".color(COLOR_TURQUOISE), COOLDOWN.toToolTipLightBlue())



class SelfPreservation extends ChannelAbilityPreset
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId, 1, true)
        this.presetManaCost(lvl -> MANACOST)
        this.presetCooldown(lvl -> COOLDOWN)
        this.setHeroAbility(false)
        this.presetTargetType(lvl -> 0)
        this.setHotkeyNormal(hotkey)
        this.setName(TOOLTIP_NORM)
        this.presetTooltipNormal(lvl -> makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED)
        this.setIconNormal(Icons.bTNRegenerate)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setFollowThroughTime(1, 1)
        this.setArtDuration(1, 2)
        this.setArtTarget("")
        this.setArtCaster(LocalAbilities.selfPreservation)
        this.setTargetAttachmentPoint("overhead")
        this.setCasterAttachmentPoint1("")
        this.setBaseOrderID(1, "fanofknives")

@compiletime function createAntiMagic()
    new SelfPreservation(ABILITY_SELF_PRESERVATION, "S", new Pair(1, 1))

function onCast()
    let caster = GetSpellAbilityUnit()
    let owner = caster.getOwner()
    let totalStatsRaiseAmount = BASE_STATS_RAISE_AMOUNT+caster.getInt(true)
    new HealingInstance(caster, caster, totalStatsRaiseAmount, HealingType.ABILITY)
    //caster.setHP(caster.getHP() + totalStatsRaiseAmount)
    caster.setMana(caster.getMana() + totalStatsRaiseAmount)
    owner.setGold(min(owner.getGold() + totalStatsRaiseAmount.toInt(), gameConfig.getHeatMaximum()))

init
    registerSpellEffectEvent(ABILITY_SELF_PRESERVATION) ->
        onCast()
