package RangedHeal

// Standard-library imports:
import Assets
import ClosureEvents
import ChannelAbilityPreset
import ClosureTimers

// Third-party imports:
import Lodash

// Local imports:
import HealingSystem
import LocalObjectIDs
import ToolTipsUtils
import ColorUtils

//let ABILITY_ID = 'XPRH'
let CAST_RANGE = 6000.
let BASE_HEALING_AMOUNT = 15.
let COOLDOWN = 10.
let MANACOST = 25
let HEAL_INT_MULTIPLIER = 0.8
let TOOLTIP_NORM = "Ranged heal"
let TOOLTIP_EXTENDED = "Heals a friendly unit for {0}+{1}% of your {2}. Can be casted from very far away. \nHas {3} seconds cooldown."
                        .format(BASE_HEALING_AMOUNT.toToolTipGreen(), (HEAL_INT_MULTIPLIER*100).toToolTipGreen(), 
                        "Intelligence".color(COLOR_TURQUOISE), COOLDOWN.toToolTipLightBlue())

let HEAL_HIT_FX = Abilities.healTarget


class RangedHeal extends ChannelAbilityPreset
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId, 1, true)
        this.setIconNormal(Icons.bTNHeal)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.presetCastRange(lvl -> CAST_RANGE)
        this.presetCastingTime(lvl -> 0)
        this.presetFollowThroughTime(lvl -> 0.275)
        this.presetCooldown(lvl -> COOLDOWN)
        this.presetManaCost(lvl -> MANACOST)
        this.presetTargetType(lvl -> 1)
        this.presetTargetsAllowed(lvl -> commaList(
            TargetsAllowed.air,
            TargetsAllowed.ground,
            TargetsAllowed.friend,
            TargetsAllowed.vulnerable,
            TargetsAllowed.invulnerable,
            TargetsAllowed.self,
            TargetsAllowed.organic,
            TargetsAllowed.nonancient,
            TargetsAllowed.neutral
        ))
        this.presetOptions(lvl -> 1)
        this.presetBaseOrderID(lvl -> "heal")
        this.setHeroAbility(false)
        this.setHotkeyNormal(hotkey)
        this.setName(TOOLTIP_NORM)
        this.presetTooltipNormal(lvl -> makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED)

@compiletime function createRangedHeal()
    new RangedHeal(ABILITY_RANGED_HEAL, "R", new Pair(3, 0))

function onCast(unit caster, unit target)
    let totalHealingAmount = BASE_HEALING_AMOUNT+caster.getInt(true)
    new HealingInstance(target, caster, totalHealingAmount, HealingType.ABILITY)
    let fx = target.addEffect(HEAL_HIT_FX, "origin")
    doAfter(1.5) ->
        fx.destr()

init
    EventListener.onTargetCast(ABILITY_RANGED_HEAL, (unit caster, unit target) -> onCast(caster, target))
