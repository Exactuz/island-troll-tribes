package StealMeat

// Standard-library imports:
import Assets
import BuffObjEditing
import ClosureTimers
import ClosureEvents
import ClosureForGroups
import ChannelAbilityPreset
import GroupUtils
import HashMap
import HashSet
import InstantDummyCaster
import OnUnitEnterLeave
import OrderIds

// Third-party imports:
import Lodash

// Local imports:
import LocalObjectIDs
import ToolTipsUtils
import UnitExtensions
import DamageListeners
import HealingSystem
import ThiefsPocket
import ColorUtils
import LocalAssets

let TT_STEAL_MEAT ="Steal a piece of meat from an enemy. If your inventory is full, it is gonna be automatically consumed."+
                    "\n30 ".color(ENERGY_COLOR)+ "seconds cooldown."
let TT_TELE_STEAL_MEAT ="\n\nTele-thief's mastery over dimentional magic greatly increases range of this ability".color(ENERGY_COLOR)

@compiletime function createStealMeatAbility()
    new ChannelAbilityPreset(ABILITY_STEAL_MEAT, 1, true)
        ..presetTooltipNormal(lvl -> "Steal Meat")
        ..presetTooltipNormalExtended(lvl-> TT_STEAL_MEAT)        
        ..presetCastingTime(lvl ->0.1)
        ..presetCastRange(lvl -> 150)
        ..presetCooldown(lvl -> 30)
        ..presetHotkey("R")
        ..setButtonPositionNormalX(3)
        ..setButtonPositionNormalY(0)
        ..presetTargetTypes(Targettype.UNIT)
        ..setIconNormal(LocalIcons.bTNStealMeat)
        ..presetTargetsAllowed(lvl ->commaList(
            TargetsAllowed.ground,
            TargetsAllowed.enemies,
            TargetsAllowed.vulnerable,
            TargetsAllowed.alive,
            TargetsAllowed.organic,
            TargetsAllowed.hero
        )) 



@compiletime function createTeleStealMeatAbility()
    new ChannelAbilityPreset(ABILITY_TELE_STEAL_MEAT, 1, true)
        ..presetTooltipNormal(lvl -> "Tele-Steal Meat")
        ..presetTooltipNormalExtended(lvl-> TT_STEAL_MEAT+TT_TELE_STEAL_MEAT)   
        ..presetCastingTime(lvl ->0.1)
        ..presetCastRange(lvl -> 400)
        ..presetCooldown(lvl -> 30)
        ..presetHotkey("R")
        ..setButtonPositionNormalX(3)
        ..setButtonPositionNormalY(0)
        ..presetTargetTypes(Targettype.UNIT)
        ..setIconNormal(LocalIcons.bTNTeleStealMeat)
        ..presetTargetsAllowed(lvl ->commaList(
            TargetsAllowed.ground,
            TargetsAllowed.enemies,
            TargetsAllowed.vulnerable,
            TargetsAllowed.alive,
            TargetsAllowed.organic,
            TargetsAllowed.hero
        ))



function onCast(unit caster, unit target)
    let itm = target.getItemById(ITEM_COOKED_MEAT)
    if itm == null
        nullTimer() -> 
            caster.endAbilityCooldown(ABILITY_STEAL_MEAT)
            caster.endAbilityCooldown(ABILITY_TELE_STEAL_MEAT)
        return
    let charges = itm.getCharges()
    if charges > 1
        itm.setCharges(charges-1)
    else 
        itm.remove()
    for itm2 in caster.getInventory()
        if itm2.getTypeId() == ITEM_COOKED_MEAT and itm2.getCharges()<10
            itm2.setCharges(itm2.getCharges()+1)
            return
    if caster.isInventoryFull()
        new HealingInstance(caster, 50, HealingType.ITEM)
        return
    let itm3 = createItem(ITEM_COOKED_MEAT, caster.getPos())
    stolenItems.add(itm3)
    caster.addItemHandle(itm3)


init 
    EventListener.onTargetCast(ABILITY_STEAL_MEAT) (unit caster, unit target) ->
        onCast(caster, target)
    EventListener.onTargetCast(ABILITY_TELE_STEAL_MEAT) (unit caster, unit target) ->
        flashEffect(Abilities.blinkCaster, target.getPos())
        onCast(caster, target)        
        
    