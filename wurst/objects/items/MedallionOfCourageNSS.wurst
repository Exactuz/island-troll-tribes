package MedallionOfCourageNSS

// Stanard library imports:
import Assets
import ChannelAbilityPreset
import ItemObjEditing
import RegisterEvents

// Third Party imports:
import SimError

// Local imports:
import Classes
import ColorUtils
import Items
import LocalObjectIDs
import LinkedList
import TrollUpgrade
import HashMap
import ClosureTimers
import ObjectIDManager
import CustomItemDefinition
import LocalObjectIDs2
import PlayerExtensions
import Experience

// The description for the item.
let DESCRIPTION = "" +
    "Unique|n".color(COLOR_PURPLE) +
    "A strange medallion that installs courage in its bearer.|n" +
    "Hunter, Gatherer, Scout, and Thief subclasses can use this to upgrade directly into the final form.|n".color(COLOR_GOLD) +
    "Other subclasses can use this to upgrade to level 8.".color(COLOR_GOLD)

let CLASSES_ALLOWED = asList(UNIT_WARRIOR, UNIT_TRACKER, UNIT_RADAR_GATHERER, UNIT_HERB_MASTER, UNIT_TELETHIEF, UNIT_ROGUE, UNIT_HAWK, UNIT_DRUID)

IterableMap<int, int> trollUpgradeMap = new IterableMap<int, int>
    ..put(UNIT_WARRIOR, UNIT_JUGGERNAUT)
    ..put(UNIT_TRACKER, UNIT_JUGGERNAUT)
    ..put(UNIT_RADAR_GATHERER, UNIT_OMNIGATHERER)
    ..put(UNIT_HERB_MASTER, UNIT_OMNIGATHERER)
    ..put(UNIT_TELETHIEF, UNIT_ASSASSIN)
    ..put(UNIT_ROGUE, UNIT_ASSASSIN)
    ..put(UNIT_HAWK, UNIT_SPY)
    ..put(UNIT_DRUID, UNIT_TREEFORM)

@compiletime function createMedallionAbility()
    createItemBerserkCast(ABILITY_SUPERSUB_COURAGE_NSS)


public constant ITEM_MEDALLION_COURAGE_NSS_DUMMY_UNIT =compiletime(UNIT_ID_GEN.next())..registerObjectID("ITEM_MEDALLION_COURAGE_NSS_DUMMY_UNIT")
public constant ITEM_MEDALLION_COURAGE_NSS_DEFINITION = compiletime(new CustomItemDefinition(ITEM_MEDALLION_COURAGE_NSS)
        ..setAbilities(commaList(ABILITY_SUPERSUB_COURAGE))
        ..setName("Medallion of Courage")
        ..setLumberCost(50)
        ..setDescription(DESCRIPTION)
        ..setTooltipExtended(DESCRIPTION)
        ..setInterfaceIcon(Icons.bTNMedalionOfCourage)
        ..setModelUsed(Objects.potofGold)
        ..setCanBeSoldToMerchants(false)    
        ..constructItemShopDummyUnit(ITEM_MEDALLION_COURAGE_NSS_DUMMY_UNIT)
        )


// Validates and unlocks the research corresponding to the Medallion of Courage.
function onUse(unit caster, item target)
    // Exit if the item used is not relevant.
    if target.getTypeId() != ITEM_MEDALLION_COURAGE_NSS
        return
    // Cache the state of the the operation.
    let owner = caster.getOwner()
    let unitId = caster.getTypeId()

    if unitId.getTrollClassType() == ClassType.SUPER_CLASS
        simError(owner, "You have already reached your final form.")
        return 
        
    if unitId.getTrollClassType() == ClassType.BASE_CLASS
        simError(owner, "You need to have chosen a subclass to use this item.")
        return    

    // Block certain classes from using the medallion.
    if not CLASSES_ALLOWED.has(unitId)
        caster.setLevel(8, true)
        target.remove()
        return


    // Consume the item manually, having verified it can be used.
    target.remove()

    upgradeUnit(caster, trollUpgradeMap.get(caster.getTypeId()))
    doAfter(ANIMATION_PERIOD) ->
        let newLevel = max(caster.getOwner().getTribe().levelCap-1, nsModeStartingLevelCap)
        caster.setLevel(newLevel, true)


init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_USE_ITEM) ->
        onUse(EventData.getManipulatingUnit(), EventData.getManipulatedItem())
