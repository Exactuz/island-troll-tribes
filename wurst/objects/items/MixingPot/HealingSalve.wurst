package HealingSalve

// Standard lib Imports:
import Assets
import ChannelAbilityPreset

// Local lib Imports:
import Items
import LocalAssets
import LocalItemObjEditing
import LocalObjectIDs
import ToolTipsUtils
import ClosureEvents
import ClosureTimers
import HealingSystem
import BuffObjEditing
import DamageEvent
import DamageListeners

public constant HEALING = 120.
public constant CAST_RANGE = 600.
public constant NAME = "Healing Salve"
public constant ICON =Icons.bTNLesserRejuvPotion
public constant COOLDOWN = 5.
public constant DURATION = 8.
let HEAL_BUFF_TT   = "This unit benefits from Health Rejuvenation; it is healing health points over time." 
public constant TARGETS_ALLOWED = commaList(
                            TargetsAllowed.friend,
                            TargetsAllowed.ground,
                            TargetsAllowed.hero,
                            TargetsAllowed.invulnerable,
                            TargetsAllowed.organic,
                            TargetsAllowed.self,
                            TargetsAllowed.vulnerable
                            )

public constant TT_HEALING_SALVE = "" +
    "A Healing Salve made from Athelas Seed. "+
    "Restores {0} Health points over {1} seconds to a target ally. ".format(HEALING.toToolTipGreen(), DURATION.toToolTipLightBlue())+
    "\nEffect is canceled on being attacked."

@compiletime function createAbilityRejuvenationHealth() returns AbilityDefinitionRejuvinationcreep
    return new AbilityDefinitionRejuvinationcreep(ABILITY_HEALING_SALVE)
    ..setName(NAME)
    ..setBuffs(1, BUFF_HEALING_SALVE.toRawCode())       
    //..presetTargetTypes(Targettype.UNIT)
    ..setArtTarget(Abilities.scroll_Regen_Target)
    ..setArtEffect("")
    ..setArtSpecial("")
    ..setHitPointsGained(1, 1)
    ..setManaPointsGained(1, 0)
    ..setManaCost(1, 0)
    ..setCooldown(1, 0)
    ..setIconNormal(LocalIcons.bTNBronzeBowlFullGreen)
    //..setArtTarget(Abilities.healingSalveTarget)
    ..setCastRange(1, CAST_RANGE)
    ..setTargetsAllowed(1, TARGETS_ALLOWED)



@compiletime function createHealingSalve() returns ItemDefinition
    return createPerishableItem(ITEM_HEALING_SALVE)
        ..setInterfaceIcon(LocalIcons.bTNBronzeBowlFullGreen)
        ..setModelUsed(LocalItems.healingSalve)
        ..setNameEnhance(NAME)
        ..setTooltipExtended(TT_HEALING_SALVE)
        ..setDescription(TT_HEALING_SALVE)
        ..setLumberCost(5)
        ..setNumberofCharges(3)
        ..setAbilities(commaList(ABILITY_HEALING_SALVE))
        ..setScalingValue(0.7)
        ..setCooldownGroup(commaList(ABILITY_HEALING_SALVE))
        ..setStockReplenishInterval(120)

@compiletime function createBuffRejuvenationHealth() returns BuffDefinition
    return new BuffDefinition(BUFF_HEALING_SALVE, BuffIds.rejuvenation) // Rejuvination
        ..setName(1, "Healing Salve")
        ..setIcon(LocalIcons.bTNBronzeBowlFullGreen)
        ..setArtTarget(1, Abilities.scroll_Regen_Target)
        ..setTooltipNormal(1, "Health Rejuvenation")
        ..setTooltipNormalExtended(1, HEAL_BUFF_TT)
        ..setEditorSuffix(1, "(Wurst)")

function onHealingSalve(unit caster, unit target)
    doPeriodicallyTimed(ANIMATION_PERIOD, DURATION) (CallbackCounted cb) ->
        if not target.hasAbility(BUFF_HEALING_SALVE)
            destroy cb
        new HealingInstance(target, caster,  (HEALING/DURATION)*ANIMATION_PERIOD, HealingType.ITEM, false)
        
        

init
    EventListener.onTargetCast(ABILITY_HEALING_SALVE) (unit caster, unit target) ->
        doAfter(ANIMATION_PERIOD) -> 
            onHealingSalve(caster, target)
        
    DamageEvent.addListener(DamageListenerPriorities.PANTHER_INSTINCT castTo int) ->
        if DamageEvent.getTarget().hasAbility(BUFF_HEALING_SALVE)
            and DamageEvent.getType() == DamageType.ATTACK or DamageEvent.getType() == DamageType.SPELL
            DamageEvent.getTarget().removeAbility(BUFF_HEALING_SALVE)
        
