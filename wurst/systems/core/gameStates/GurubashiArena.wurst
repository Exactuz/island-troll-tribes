package GurubashiArena

// Standard library imports:
import ClosureTimers

// Local imports:
import ColorUtils
import GameStates
import GameConfig
import TimerUtils
import GameState
import PlayerExtensions
import Tribe
import Objective
import ReduceHealing
import LocalObjectIDs
import Respawn
import initlater GurubashiArenaBorders
import PhilosophersStone
import UnitExtensions

public timer GURUBASHI_ARENA_OPEN_TIMER      

function onEnter()
    let tribes = Tribe.getTribes()
    print("Gurubashi Arena has begun!".color(GENERAL_COLOR))
    createUnit(players[PLAYER_NEUTRAL_PASSIVE], UNIT_BONFIRE, OBJECTIVE_POS)
    updateBorderAccessOnDuel()

    for tribe in tribes
        let index = tribes.indexOf(tribe)
        vec2 pos
        if index == 0
            pos = GURUBASHI_ARENA_ENTRANCE_LEFT.randomPoint()
        else 
            pos = GURUBASHI_ARENA_ENTRANCE_RIGHT.randomPoint()
        for member in tribe.getMembers()
            let troll = member.getTroll()
            if not troll.isAlive()
                revive(troll, 0.)
            doAfter(ANIMATION_PERIOD) ->
                resetTroll(troll)
                troll.pause()
                troll.setPos(pos)
                member.panCamToTimed(troll, 0.1)
                doAfter(1.) -> 
                    closeGates()
                doAfter(3.0) -> 
                    troll.unpause() 

public function closeGates()
    for each in gurubashiArenaGates
        each.restoreLife(each.getMaxLife(), true)
        each.setAnimation("stand")
        each.setInvulnerable(true)

function resetTroll(unit troll)
    if troll.isAlive()
        troll
        ..pause()
        ..removeBuffs(true, true)
        ..setHP(troll.getMaxHP())
        ..setMana(troll.getMaxMana())
        ..setHeat(gameConfig.getMaxHeat())
        ..resetCooldown()
        ..reduceHealingReduction(9999.)
        if troll.getTypeId() == UNIT_ALCHEMIST
            let instance = PhilosophersStone.getInstance(troll)
            if instance != null
                instance.setMaximumCharges()

function updateBorderAccessOnGatesOpen()
    for tribe in Tribe.getTribes()
        let bordersAccess = GurubashiArenaBorders.getInstance(tribe)
        bordersAccess.insideGurubashiArena = true
        bordersAccess.outsideGurubashiArena = true

function updateBorderAccessOnDuel()
    for tribe in Tribe.getTribes()
        let bordersAccess = GurubashiArenaBorders.getInstance(tribe)
        bordersAccess.insideGurubashiArena = true
        bordersAccess.outsideGurubashiArena = false

init
    GameStates.gurubashiArena.onEnter() (GameState state) ->
        onEnter()
        
    GameStates.gameplay.onEnter() (GameState state) -> 
        if gameConfig.getObjective() != Objective.EarlyDuel
            let time = gameConfig.getGurubashiArenaAfter()
            GURUBASHI_ARENA_OPEN_TIMER = getTimer()..doAfter(time.toReal()) ->
                openGurubashiGates()
                updateBorderAccessOnGatesOpen()