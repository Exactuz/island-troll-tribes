package ForcedDuel

// Standard library imports:
import ClosureTimers

// Local imports:
import ColorUtils
import GameStates
import GameConfig
import PlayerExtensions
import TimerUtils
import Tribe
import UnitExtensions
import SoundUtils

public timer DUEL_TIMER

function moveTrollsToArena()
    print("Your duel has begun!".color(GENERAL_COLOR))

    let tribes = Tribe.getTribes()

    for tribe in tribes
        let pos = gg_rct_Dualing_Area.randomPoint()

        for member in tribe.getMembers()
            let troll = member.getTroll()
            if troll.isAlive()
                troll
                ..pause()
                ..removeBuffs(true, true)
                ..setPos(pos)
                ..setHP(troll.getMaxHP())
                ..setMana(troll.getMaxMana())
                ..setHeat(gameConfig.getMaxHeat())

                member.panCamToTimed(troll, 0.1)

    doAfter(3.0) ->
        for tribe in tribes
            for member in tribe.getMembers()
                let troll = member.getTroll()
                if troll.isAlive()
                    troll.unpause()

init
    GameStates.gameplay.onEnter() state ->
        if gameConfig.isForcedDuelEnabled()
            let timeout = gameConfig.getForcedDuelAfter()
            DUEL_TIMER = getTimer()..doAfter(timeout.toReal(), () -> moveTrollsToArena())
            doAfter(timeout.toReal()-300) ->
                print("Duel will start in 5 minutes".color(COLOR_RED))
            doAfter(timeout.toReal()-120) ->
                new SoundDefinition(Sounds.arrangedTeamInvitation).play()
                print("Duel will start in 2 minutes".color(COLOR_RED))                
                
