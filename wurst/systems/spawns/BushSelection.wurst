package BushSelection
import HashMap
import RegisterEvents
import InventoryUtils
import Classes
import UnitUtils
import LocalObjectIDs
import PlayerExtensions
import ThiefsPocket

let MAX_DIST = 300

let map = new HashMap<int, int>()
    ..put(UNIT_THIEFS_BUSH, UNIT_THIEF)
    ..put(UNIT_SCOUTS_BUSH, UNIT_SCOUT)

function transferItems(unit u, player p)
    // Look up the type ID for the units that can use the bush.
    let targetID = map.get(u.getTypeId())
    // Exit if the selection unit is not a bush.
    if targetID == 0
        return

    // Retrieve the nearest unit able to be transfered to.
    let match = findNearestUnit(u.getPos(), MAX_DIST.toReal()) target ->
        return (
                target.getOwner().isAllyOf(p)
            and targetID == target.getTrollBaseClass()
        )

    // Deliver the items
    if match != null
        //Deliver first item to Thief's Pocket if its empty
        if match.getTrollBaseClass() == UNIT_THIEF
            for slot = 0 to 5
                let itm = u.itemInSlot(slot)
                if itm != null 
                    instances.get(match).onTargetOrder(itm)
                    break

        u.transferItems(match)


init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SELECTED) ->
        transferItems(GetTriggerUnit(), GetTriggerPlayer())
